= Протоколы
Rich Hickey
2015-01-01
:type: reference
:toc: macro
:toc-title: Содержание
:icons: font
:prevpagehref: multimethods
:prevpagetitle: Multimethods and Hierarchies
:nextpagehref: metadata
:nextpagetitle: Metadata

ifdef::env-github,env-browser[:outfilesuffix: .adoc]

toc::[]

== Мотивация

Clojure написана в терминах абстракций. Существуют абстракции для последовательностей, коллекций, вызываемых функций и другие. Также Clojure поддерживает множество реализаций этих абстракций. Эти абстракции определены интерфейсами Java, а реализации - Java-классами. И хотя такой подход и ускоряет начальную загрузку языка, это оставляет Clojure без аналогичных абстракций и низкоуровневых средств реализации. <<protocols#,Протоколы>> и <<datatypes#,типы данных>> добавляют мощные и гибкие механизмы для объявления абстракций и структур данных гораздо более удобные, чем возможности платформы Java.

Существуют следующие причины создания протоколов:

* Предоставить высокопроизводительную конструкцию с динамическим полиморфизмом как альтернативу интерфейсам
* Поддерживать лучшее из интерфесов
** только спецификация, без реализации
** один тип может реализовывать несколько протоколов
* При этом избежать некоторых недостатков
** Какие интерфейсы реализовываются - это выбор, осуществляющийся автором во время проектирования и это не может быть расширено позже (хотя вставки интерфейсов могут в конечном итоге обратиться к этому)
** реализация интерфейса создает отношения между классами и иерархию
* Избежать "проблемы выражения" позволив независимо расширять множество типов, протоколов и реализаций протоколов
** делать это без оберток и адаптеров
* Поддерживать 90% случаев мультиметодов во время предоставления высокоуровневой абстракции/организации

[ВНИМАНИЕ]
Протоколы были добавлены в Clojure 1.2.

== Основы

Протокол - это именованное множество методов и их сигнатур, определенных с помощью http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/defprotocol[defprotocol]:

[source,clojure]
----
(defprotocol AProtocol
  "A doc string for AProtocol abstraction"
  (bar [a b] "bar docs")
  (baz [a] [a b] [a b c] "baz docs"))
----

* Никаких реализаций не предосталвяется
* Комментарии могут специфицировать как протокол, так и функции
* Код выше порождает множество полиморфичных функций и объект протокола
** все они будут принадлежать пространству имен, в котором было выполнено объявление протокола
* Получаемые функции отправляют по типу их первого аргумента и поэтому должны иметь хотя бы один аргумент
* defprotocol - динамический и не требует AOT-компиляции.

http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/defprotocol[defprotocol] автоматически генерирует соответствующий интерфейс, с тем же именем, как у протокола, то есть для протокола my.ns/Protocol будет сгенерирован интерфейс my.ns.Protocol. Интерфейс будет иметь методы, соответсующими функциям протокола и протокол будет автоматически работать с экземплярами этого интерфейса.

Заметим, что вам не необходимо использовать этот интерфейс с
 http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/deftype[deftype] ,
 http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/defrecord[defrecord] ,
 или http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/reify[reify],
 так как они напрямую поддерживают протокол:

[source,clojure]
----
(defprotocol P
  (foo [x])
  (bar-me [x] [x y]))

(deftype Foo [a b c]
  P
  (foo [x] a)
  (bar-me [x] b)
  (bar-me [x y] (+ c y)))

(bar-me (Foo. 1 2 3) 42)
= > 45

(foo
 (let [x 42]
   (reify P
     (foo [this] 17)
     (bar-me [this] x)
     (bar-me [this y] x))))

> 17
----

Java клиент желающий участвовать в протоколе может наиболее эффективно сделать это реализовав сгенерированный протоколом интерфейс.

Внешние реализации протокола (которые необходимы когда вы хотите привлечь класс или тип, находящийся вне вашего контроля, для взаимодействия с протоколом) могет быть предоставлены с помощью конструкции http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/extend[extend]:

[source,clojure]
----
(extend AType
  AProtocol
   {:foo an-existing-fn
    :bar (fn [a b] ...)
    :baz (fn ([a]...) ([a b] ...)...)}
  BProtocol
    {...}
...)
----

extend берет тип/класс (или интерфейс, см. ниже) одну или более пар протокол + ассоциативный массив функций.

* Расширит полиморфизм методов протокола, чтобы вызвать предоставленные функции, когда AType был предоставлен в качестве первого аргумента
* Ассоциативный массив функций содержит соответствия между ключевыми словами-именами и обычными функциями
** это облегчает переиспользование существующих функций и ассоциативных массивов
* Вы можете реализовать протокол на интерфейсе
** это в первую очередь облегчает взаимодействие с Java-платформой
** но открывает дверь в случайное множественное наследование реализаций
*** поскольку класс может реализовывать более одного интерфейса, которые реализуют протокол
*** если один интерфейс расширяет другой, используется первый, иначе неопределено, какой из них будет использован.
* Реализация функции может предполагать первый аргумент экземпляром AType
* Вы можете реализовать протокол на _**nil**_
* Чтобы определить реализацию протокола по-умолчанию, (отличную от nil) просто используйте Object

Protocols are fully reified and support reflective capabilities via http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/extends%3F[extends?] , http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/extenders[extenders] , and http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/satisfies%3F[satisfies?] .

* Note the convenience macros http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/extend-type[extend-type] , and http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/extend-protocol[extend-protocol]
* If you are providing external definitions inline, these will be more convenient than using *extend* directly

[source,clojure]
----
(extend-type MyType
  Countable
    (cnt [c] ...)
  Foo
    (bar [x y] ...)
    (baz ([x] ...) ([x y zs] ...)))

  ;expands into:

(extend MyType
  Countable
   {:cnt (fn [c] ...)}
  Foo
   {:baz (fn ([x] ...) ([x y zs] ...))
    :bar (fn [x y] ...)})
----
