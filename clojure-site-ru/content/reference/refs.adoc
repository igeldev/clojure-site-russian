= Ссылки и транзакции
Rich Hickey
2015-01-01
:type: reference
:toc: macro
:toc-title: Содержание
:icons: font
:prevpagehref: vars
:prevpagetitle: Vars and Environments
:nextpagehref: agents
:nextpagetitle: Agents

ifdef::env-github,env-browser[:outfilesuffix: .adoc]

toc::[]

В то время как <<vars#,переменные>> обеспечивают безопасное использование изменяемых хранилищ через изоляцию потоков, транзакционные ссылки обеспечивают безопасное коллективное использование изменяемых хранилищ с помощью https://ru.wikipedia.org/wiki/Программная_транзакционная_память[программной транзакционной памяти] (software transactional memory, SТМ). Ссылки привязаны к одному хранилищу в течении своей жизни и позволяют изменять его содержимое только внутри транзакции.

Понять транзакции Clojure должно быть просто, если вы когда-нибудь использовали транзакции баз данных - они гарантируют, что все действия со ссылками атомарны, консистентны и изолированы. Атомарны означает, что каждое изменение ссылок сделанное внутри транзакции либо происходит целиком либо не происходит вовсе. Консистентны означает, что каждое новое значение может быть проверено с помощью функции перед выполнением транзакции. Изолированы означает, что пока транзакция не завершена, другие транзацкции не увидят результат её работы. Еще одно свойство общее с STM (программной трензакционной памятью) - если во время выполнения транзакции возникает конфликт - она автоматически повторяется.

Существуют несколько способов реализации STM (программной транзакционной памяти) - блокирующая/пессимистичная, безблокировочная/оптимистическая, гибриды. В этом направлении разработки ведутся до сих пор. Для реализации STM Clojure использует http://en.wikipedia.org/wiki/Multiversion_concurrency_control[управление конкурентным доступом с помощью многоверсионности] с адаптивной историей для http://en.wikipedia.org/wiki/Snapshot_isolation[изоляции версий] и предоставляет отдельную функцию http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/commute[commute].

На практике это означает:

. Все операции чтения ссылок будут производиться в версии "мира ссылок", соответствующей началу транзакции (её "точка чтения"). Транзакции _будут_ видеть все свои изменения, называемые _значение-внутри-транзакции_.
. Все изменения, производимые со ссылками в течении транзакции (с помощью http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/ref-set[ref-set], http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/alter[alter] или http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/commute[commute]) будут применены в "мире ссылок" в один момент времени ("точке записи" транзакции).
. Другие транзакции не смогут модифицировать ссылки, изменение которых было запрещено функциями _**ref-set**_ / _**alter**_ / _**ensure**_.
. Другие транзакции _могут_ модифицировать любые ссылки, изменение которых было разрешено функцией _**commute**_. Это не должно вызывать проблем, так как функция, применяемая _**commute**_ должна быть коммутативна.
. Операции чтения и объединения ссылок (с помощью _**commute**_) никогда не блокируют другие операции, а том числе и операции записи ссылок.
. Операции записи не блокируют операции чтения и объединения.
. Внутри транзакций необходимо избегать операций ввода/вывода, а также других действий, имеющих подобные "скрытые последствия", так как транзакции могут _повторно исполняться_. Макрос http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/io![io!] может быть использован, чтобы запретить выполнять подобные операции внутри транзакций.
. Если условие корректности значения одной ссылки зависит от значения другой и при этом первая изменяется внутри транзакции, а вторая - нет, то вторая ссылка может быть "защищена" от изменений с помощью http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/ensure[ensure]. Защищенные таким образом ссылки не будут модифицироваться другими транзациями (пункт 3) и их значение не будет изменено в точке записи транзакции (пункт 2).
. The Clojure MVCC STM is designed to work with the persistent collections, and it is strongly recommended that you use the Clojure collections as the values of your Refs. Since all work done in an STM transaction is speculative, it is imperative that there be a low cost to making copies and modifications. Persistent collections have free copies (just use the original, it can't be changed), and 'modifications' share structure efficiently. In any case:
. The values placed in Refs _must be, or be considered, immutable_!! Otherwise, Clojure can't help you.

== Example
In this example a vector of references to vectors is created, each containing (initially sequential) unique numbers. Then a set of threads are started that repeatedly select two random positions in two random vectors and swap them, in a transaction. No special effort is made to prevent the inevitable conflicts other than the use of transactions.
[source,clojure]
----
(defn run [nvecs nitems nthreads niters]
  (let [vec-refs (vec (map (comp ref vec)
                           (partition nitems (range (* nvecs nitems)))))
        swap #(let [v1 (rand-int nvecs)
                    v2 (rand-int nvecs)
                    i1 (rand-int nitems)
                    i2 (rand-int nitems)]
                (dosync
                 (let [temp (nth @(vec-refs v1) i1)]
                   (alter (vec-refs v1) assoc i1 (nth @(vec-refs v2) i2))
                   (alter (vec-refs v2) assoc i2 temp))))
        report #(do
                 (prn (map deref vec-refs))
                 (println "Distinct:"
                          (count (distinct (apply concat (map deref vec-refs))))))]
    (report)
    (dorun (apply pcalls (repeat nthreads #(dotimes [_ niters] (swap)))))
    (report)))

----
When run, we see no values get lost or duplicated in the shuffle:
[source,clojure]
----
(run 100 10 10 100000)

([0 1 2 3 4 5 6 7 8 9] [10 11 12 13 14 15 16 17 18 19] ...
 [990 991 992 993 994 995 996 997 998 999])
Distinct: 1000

([382 318 466 963 619 22 21 273 45 596] [808 639 804 471 394 904 952 75 289 778] ...
 [484 216 622 139 651 592 379 228 242 355])
Distinct: 1000
----

== Related functions

Create a Ref: http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/ref[ref]

Examine a Ref: http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/deref[deref] _(see also the +@+ <<reader#,reader>> macro)_

Transaction macros: http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/dosync[dosync] http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/io![io!]

Allowed only in a transaction: http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/ensure[ensure] http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/ref-set[ref-set] http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/alter[alter] http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/commute[commute]

Ref validators: http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/set-validator![set-validator!] http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/get-validator[get-validator]
